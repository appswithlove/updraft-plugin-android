import com.vanniktech.maven.publish.GradlePlugin
import com.vanniktech.maven.publish.JavadocJar

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.vanniktech.maven.publish'
    id 'com.gradle.plugin-publish'
}

group GROUP
version VERSION_NAME

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation gradleTestKit()
    testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testRuntimeOnly 'cglib:cglib-nodep:3.3.0'
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}

tasks.withType(Test).configureEach {
    testLogging.events = ["passed", "skipped", "failed", "standardOut", "standardError"]
}

mavenPublishing {
    configure(new GradlePlugin(new JavadocJar.Javadoc(), true))
}

def pluginId = project.findProperty("GROUP") ?: ""
def pluginName = project.findProperty("POM_NAME") ?: ""
def pluginDescription = project.findProperty("POM_DESCRIPTION") ?: ""
def pluginVersion = project.findProperty("VERSION_NAME") ?: ""
def pluginWebsite = project.findProperty("POM_URL") ?: ""
def pluginVcsUrl = project.findProperty("POM_SCM_URL") ?: ""

pluginBundle  {
    website = pluginWebsite
    vcsUrl = pluginVcsUrl

    plugins {
        updraft {
            id = pluginId
            displayName = pluginName
            description = pluginDescription
            version = pluginVersion
            tags = ['updraft', 'android']
        }
    }
}
